#version 300 es
precision mediump float;

uniform sampler2D u_texture;
uniform float u_time;
uniform float u_tiling;
uniform float u_flowSpeed;
uniform float u_animationSpeed;

in vec2 v_texcoord;
in vec3 v_normal;
in vec3 v_fragPos;
in vec3 v_cameraPos;

struct Material{
   vec3 ambient;
   vec3 diffuse;
   vec3 specular;
   float shininess;
};

uniform Material material;

struct Light{
   vec3 position;
   vec3 ambient;
   vec3 diffuse;
   vec3 specular;
};

uniform Light light;

out vec4 FragColor;

void main() {
    // ambient
    vec3 ambient = light.ambient * material.ambient;
  	
    // diffuse 
    vec3 norm = normalize(v_normal);
    vec3 lightDir = normalize(light.position - v_fragPos);
    float diff = max(dot(norm, lightDir), 0.0);
    vec3 diffuse = light.diffuse * (diff * material.diffuse);
    
    // specular
    vec3 viewDir = normalize(v_cameraPos - v_fragPos);
    vec3 reflectDir = reflect(-lightDir, norm);  
    float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
    vec3 specular = light.specular * (spec * material.specular);  

    vec2 tileSize = vec2(1.0 / 3.0, 1.0 / 1.0);
    vec2 uv_water1 = fract(v_texcoord * u_tiling + vec2(0.0, -u_time * u_animationSpeed)) * tileSize + vec2(0.0, 0.0) * tileSize;
    vec2 uv_water2 = fract(v_texcoord * u_tiling + vec2(0.0, 0.0)) * tileSize + vec2(1.0, 0.0) * tileSize;
    vec2 uv_foam = fract(v_texcoord * u_tiling + vec2(0.0, -u_time * u_flowSpeed)) * tileSize + vec2(2.0, 0.0) * tileSize;
    float noiseValue1 = texture(u_texture, uv_water1).r; // slå upp från noise-texturen
    float noiseValue2 = texture(u_texture, uv_water2).r; // slå upp från noise-texturen
    float noiseValue3 = texture(u_texture, uv_foam).r; // slå upp från noise-texturen
    float alpha = 0.9;
    vec3 darkBlue = vec3(0.4, 0.4, 0.8);
    vec3 lightBlue = vec3(0.4, 0.7, 1.0);
   // vattenfärger
   vec3 color1 = mix(darkBlue, lightBlue, noiseValue1);
   vec3 color2 = mix(darkBlue, lightBlue, noiseValue2);
   vec3 waterColor = (color1 + color2) * 0.5;

   // skum (vitt där texturen är vit)
   vec3 foamColor = vec3(1.0);

   // maska in skum: svart = inget, vitt = fullt skum
   float foamMask = noiseValue3;

   // blanda vatten och skum
   vec3 finalColor = mix(waterColor, foamColor, foamMask);

   vec3 result = (ambient + diffuse + specular) * finalColor;
   FragColor = vec4(result, alpha);
}