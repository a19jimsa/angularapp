precision mediump float;

uniform sampler2D u_texture;
uniform float u_time;
uniform float u_tiling;

varying vec2 v_texcoord;
varying vec3 v_normal;
varying vec3 v_fragPos;
varying vec3 v_cameraPos;

struct Material{
   vec3 ambient;
   vec3 diffuse;
   vec3 specular;
   float shininess;
};

uniform Material material;

struct Light{
   vec3 position;

   vec3 ambient;
   vec3 diffuse;
   vec3 specular;
};

uniform Light light;

void main() {
    // ambient
    vec3 ambient = light.ambient * material.ambient;
  	
    // diffuse 
    vec3 norm = normalize(v_normal);
    vec3 lightDir = normalize(light.position - v_fragPos);
    float diff = max(dot(norm, lightDir), 0.0);
    vec3 diffuse = light.diffuse * (diff * material.diffuse);
    
    // specular
    vec3 viewDir = normalize(v_cameraPos - v_fragPos);
    vec3 reflectDir = reflect(-lightDir, norm);  
    float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
    vec3 specular = light.specular * (spec * material.specular);  

    vec2 tileSize = vec2(1.0 / 2.0, 1.0 / 1.0);
    vec2 uv_water1 = fract(v_texcoord * u_tiling + vec2(0.0, u_time)) * tileSize + vec2(0.0, 0.0) * tileSize;
    vec2 uv_water2 = fract(v_texcoord * u_tiling + vec2(0.0, -u_time)) * tileSize + vec2(1.0, 0.0) * tileSize;
    float noiseValue1 = texture2D(u_texture, uv_water1).r; // sl책 upp fr책n noise-texturen
    float noiseValue2 = texture2D(u_texture, uv_water2).r; // sl책 upp fr책n noise-texturen
    float alpha = 0.9;
    vec3 darkBlue = vec3(0.4, 0.4, 0.8);
    vec3 lightBlue = vec3(0.4, 0.7, 1.0);
    vec3 color1 = mix(darkBlue, lightBlue, noiseValue1);
    vec3 color2 = mix(darkBlue, lightBlue, noiseValue2);
    vec3 finalColor = (color1 + color2) * 0.5;
    vec3 result = (ambient + diffuse + specular) * finalColor;
    gl_FragColor = vec4(result, alpha);
}